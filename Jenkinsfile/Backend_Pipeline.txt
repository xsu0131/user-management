pipeline {
    agent any

    tools {
        maven 'Maven3'
        jdk 'JDK21'
    }

    environment {
        IMAGE_NAME = "xsu0131/user-management-backend"
        IMAGE_TAG  = "${BUILD_NUMBER}"
    }

    stages {

        stage('Checkout') {
            steps {
                git branch: 'docker-support',
                    url: 'https://github.com/xsu0131/user-management.git'
            }
        }

        stage('Terraform Init') {
            steps {
                sh 'terraform -chdir=infra/terraform init -reconfigure'
            }
        }

        stage('Build Backend JAR') {
            steps {
                dir('backend') {
                    sh 'mvn clean package -DskipTests'
                }
            }
        }

        stage('Docker Build') {
            steps {
                dir('backend') {
                    sh """
                        docker build -t ${IMAGE_NAME}:${IMAGE_TAG} .
                        docker tag ${IMAGE_NAME}:${IMAGE_TAG} ${IMAGE_NAME}:latest
                    """
                }
            }
        }

        stage('Docker Push') {
            steps {
                withCredentials([
                    usernamePassword(
                        credentialsId: 'dockerhub-creds',
                        usernameVariable: 'DOCKER_USER',
                        passwordVariable: 'DOCKER_PASS'
                    )
                ]) {
                    sh """
                        echo \$DOCKER_PASS | docker login -u \$DOCKER_USER --password-stdin
                        docker push ${IMAGE_NAME}:${IMAGE_TAG}
                        docker push ${IMAGE_NAME}:latest
                    """
                }
            }
        }

        stage('Get Infrastructure IPs') {
            steps {
                script {
                    env.BACKEND_IP = sh(
                        script: 'terraform -chdir=infra/terraform output -raw backend_ip',
                        returnStdout: true
                    ).trim()

                    env.DB_IP = sh(
                        script: 'terraform -chdir=infra/terraform output -raw database_ip',
                        returnStdout: true
                    ).trim()
                }
            }
        }

        stage('Deploy Backend Container') {
            steps {
                withCredentials([
                    string(credentialsId: 'aws-access-key', variable: 'AWS_ACCESS_KEY'),
                    string(credentialsId: 'aws-secret-key', variable: 'AWS_SECRET_KEY'),
                    string(credentialsId: 'aws-s3-bucket', variable: 'AWS_S3_BUCKET'),
                    string(credentialsId: 'aws-s3-region', variable: 'AWS_S3_REGION')
                ]) {

                    sshagent(credentials: ['ec2-ssh-key']) {

                        script {

                            writeFile file: 'inventory.ini', text: """
[backend]
${env.BACKEND_IP}

[database]
${env.DB_IP}

[all:vars]
ansible_user=ubuntu
ansible_ssh_private_key_file=/home/ubuntu/.ssh/project1u.pem
ansible_python_interpreter=/usr/bin/python3
aws_access_key=${AWS_ACCESS_KEY}
aws_secret_key=${AWS_SECRET_KEY}
aws_s3_bucket=${AWS_S3_BUCKET}
aws_s3_region=${AWS_S3_REGION}
"""

                            sh """
                                ANSIBLE_HOST_KEY_CHECKING=False \
                                ansible-playbook \
                                -i inventory.ini \
                                infra/ansible/playbooks/deploy_backend_docker.yml \
                                -e image_tag=${IMAGE_TAG}
                            """
                        }
                    }
                }
            }
        }

        stage('Health Check') {
            steps {
                script {
                    sleep 20

                    def status = sh(
                        script: "curl -o /dev/null -s -w \"%{http_code}\" http://${env.BACKEND_IP}:8080/actuator/health",
                        returnStdout: true
                    ).trim()

                    echo "Health returned HTTP ${status}"

                    if (!(status in ['200','401','403'])) {
                        error "Application is not reachable"
                    }
                }
            }
        }
    }

    post {
        success {
            echo "✅ Backend deployed successfully!"
            echo "URL: http://${env.BACKEND_IP}:8080"
        }
        failure {
            echo "❌ Backend deployment failed!"
        }
        always {
            sh 'docker system prune -f'
        }
    }
}